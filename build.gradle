apply plugin: 'eclipse'
apply plugin: 'idea'

subprojects {
    apply plugin: 'java'

    version = "1.1.1"

    repositories {
      mavenLocal()
      jcenter()
    }

    dependencies {
      compile "info.picocli:picocli:2.3.0"
      compile "fastily:jwiki:1.6.1"
    }
}

project(':mtc-cli') {
    apply plugin: 'application'

    mainClassName = "mtc.CMTC"

    dependencies {
        compile project(':mtc-shared')
    }


    task doDist(type: Jar) {
      baseName = "CMTC"

      from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
      }

      manifest {
        attributes 'Main-Class': 'mtc.CMTC'
      }
      
      exclude "META-INF/*.SF", "**/LICENSE*", "**/NOTICE*", "**/maven/**"

      with jar
    }
}

project(':mtc-ui') {
    apply plugin: 'application'

    mainClassName = "mtc.App"

    dependencies {
        compile project(':mtc-shared')
    }


    task doDist(type: Jar) {
      baseName = "MTC"

      from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
      }

      manifest {
        attributes 'Main-Class': 'mtc.App'
      }
      
      exclude "META-INF/*.SF", "**/LICENSE*", "**/NOTICE*", "**/maven/**"

      with jar
    }
}

wrapper {
    gradleVersion = '4.8.1'
}