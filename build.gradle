plugins {
  id "org.openjfx.javafxplugin" version "0.0.8" apply false
  id "eclipse"
  id "idea"
}

subprojects {
  apply plugin: 'java'

  version = "1.1.1"

  repositories {
    mavenLocal()
    jcenter()
  }

  dependencies {
    compile 'info.picocli:picocli:4.0.4'
    compile 'fastily:wp-toolbox:0.0.2'
  }
}

project(':mtc-cli') {
  apply plugin: 'application'

  mainClassName = "mtc.CMTC"

  dependencies {
    compile project(':mtc-shared')
  }

  task doDist(type: Jar) {
    baseName = "CMTC"

    from {
      configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }

    manifest {
      attributes 'Main-Class': 'mtc.CMTC'
    }

    exclude "META-INF/*.SF", "**/LICENSE*", "**/NOTICE*", "**/maven/**"

    with jar
  }
}

project(':mtc-ui') {
  apply plugin: 'application'
  apply plugin: "org.openjfx.javafxplugin"

  mainClassName = "mtc.App"

  dependencies {
    compile project(':mtc-shared')
  }

  javafx {
    version = "11.0.2"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
  }

  task doDist(type: Jar) {
    baseName = "MTC"

    from {
      configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }

    manifest {
      attributes 'Main-Class': 'mtc.App'
    }

    exclude "META-INF/*.SF", "**/LICENSE*", "**/NOTICE*", "**/maven/**"

    with jar
  }
}

wrapper {
  gradleVersion = '6.0'
}